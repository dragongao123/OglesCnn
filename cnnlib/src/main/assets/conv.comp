layout (local_size_x = X_SIZE, local_size_y = Y_SIZE, local_size_z = Z_SIZE) in;

uniform int params[13];  // kennel_shape[3] input_shape[3] output_shape[3] strides[2] padding active_type
layout(binding = 0, rgba32f) readonly uniform image2D input_image;
layout(binding = 1, rgba32f) writeonly uniform image2D output_image;
layout(std430, binding = 0) buffer BufferObject {
    float kennel_data[(KENNEL_SIZE + 4) * KENNEL_AMOUNT];
};
bool isZero(ivec2 index, int[3] input_shape);
ivec2[KENNEL_AREA] getInIndexInFeatureMap(ivec3 pos, int[3] kennel_shape, int padding, int[2] strides);
float performConvolute(int num, ivec2[KENNEL_AREA] input_data_index, int[3] input_shape, int kennel_index);
ivec3 getRealStoreIndex(ivec3 pos, int[3] output_shape, int kennel_index);
ivec2 getFeatureMapIndexInTexture(int num, int width);
vec4 getKennelValue(int num, int kennel_value_index, int kennel_index, int[3] input_shape);
float relu(float i);
float sigmoid(float i);
float activeValue(int type, float v);

bool is_zero[KENNEL_AREA];

void main(void)
{
    // 分解参数
    int kennel_shape[3] = int[3](params[0], params[1] , params[2]);
    int input_shape[3] = int[3](params[3], params[4] , params[5]);
    int output_shape[3] = int[3](params[6], params[7] , params[8]);
    int strides[2] = int[2](params[9], params[10]);
    int padding = params[11];
    int active_type = params[12];

    ivec3 pos = ivec3(gl_GlobalInvocationID.xyz);
    int kennel_index = pos.z * 4;
    if (kennel_index >= output_shape[2]) {
        return;
    }
    ivec3 restore_pos = getRealStoreIndex(pos, output_shape, kennel_index);
    ivec2 input_data_index[KENNEL_AREA] = getInIndexInFeatureMap(pos, kennel_shape, padding, strides);
    int count = kennel_shape[2] / 4;
    vec4 output_vec = vec4(0.0);

    for (int i = 0; i < KENNEL_AREA; i++) {
        ivec2 index = input_data_index[i];
        is_zero[i] = isZero(index, input_shape);
    }

    for (int ii = kennel_index ; ii < kennel_index + 4; ii++) {
        if (ii < output_shape[2]) {
            float output_value = 0.0;
            for (int i=0; i < count; i++) {
                output_value += performConvolute(i, input_data_index, input_shape, ii);
            }
            int bias_index = (ii + 1) * (KENNEL_SIZE + 4) - 4;
            output_value += kennel_data[bias_index];
            output_vec[ii - kennel_index] = activeValue(active_type, output_value);
        } else {
            output_vec[ii - kennel_index] = 0.0;
        }
    }


    imageStore(output_image, restore_pos.xy,  output_vec);
}

ivec2 getFeatureMapIndexInTexture(int num, int width) {
    int xMaxCount = 1024 / width;   // 1024 是设置的存储纹理边长
    int x =0;
    int y =0;
    if (num < xMaxCount) {
        x = num;
    } else {
        x = num % xMaxCount;
        y = num / xMaxCount;
    }
    return ivec2(x,y);
}

// 获得实际存储位置 x,y表示在特征图上的坐标，z表示是第几张特征图
ivec3 getRealStoreIndex(ivec3 pos, int[3] output_shape, int kennel_index) {
    int count = kennel_index/4;
    int remain = kennel_index % 4;
    ivec2 relative_pos = pos.xy;
    ivec2 feature_map_index_in_texture = getFeatureMapIndexInTexture(count, output_shape[0]);
    ivec2 index_in_texture = relative_pos + ivec2(feature_map_index_in_texture[0]*output_shape[0], feature_map_index_in_texture[1]*output_shape[1]);
    return ivec3(index_in_texture, remain);
}

// 判断是否处于padding区域
bool isZero(ivec2 index, int[3] input_shape) {
    if (index.x < 0 || index.x >= input_shape[0]) {
        return true;
    } else if (index.y < 0 || index.y >= input_shape[1]) {
        return true;
    }
    return false;
}

// 获得本次卷积 在特征图上的坐标
ivec2[KENNEL_AREA] getInIndexInFeatureMap(ivec3 pos, int[3] kennel_shape, int padding, int[2] strides) {
    ivec2 input_data_index[KENNEL_AREA];
    for (int y=0; y < kennel_shape[1]; y++){
        for (int x=0;x < kennel_shape[0]; x++){
            input_data_index[y*kennel_shape[0] + x] = ivec2(-padding,-padding) + ivec2(x,y) + ivec2(strides[0]*pos.x, strides[1]*pos.y);
        }
    }
    return input_data_index;
}

// 在一组特征图上继续卷积运算
float performConvolute(int num, ivec2[KENNEL_AREA] input_data_index, int[3] input_shape, int kennel_index) {
    ivec2 feature_map_index_in_texture = getFeatureMapIndexInTexture(num, input_shape[0]);
    ivec2 offset = ivec2(feature_map_index_in_texture[0]*input_shape[0],feature_map_index_in_texture[1]*input_shape[1]);
    float output_value;
    for (int i = 0; i < KENNEL_AREA;i++){
        ivec2 index = input_data_index[i];
        if (!is_zero[i]) {
            ivec2 index_in_texture = index + offset;
            vec4 data = imageLoad(input_image, index_in_texture);
            vec4 k_v = getKennelValue(num, i, kennel_index, input_shape);
            output_value += dot(data, k_v);
        } else {
            output_value += 0.0;
        }
    }
    return output_value;
}

// 获得第 i~i+4 特征图 对应的kennel值
vec4 getKennelValue(int num, int kennel_value_index, int kennel_index, int[3] input_shape) {
    int start_index = kennel_index * (KENNEL_SIZE + 4) + kennel_value_index * input_shape[2] + num * 4;
    return vec4(kennel_data[start_index], kennel_data[start_index + 1], kennel_data[start_index + 2], kennel_data[start_index + 3]);

}

float relu(float i){
    if (i > 0.0){
        return i;
    } else {
        return 0.0;
    }
}

float sigmoid(float i){
    return 1.0 / (1.0 + exp(-i));
}

float activeValue(int type, float v){
    if (type == 0) {
        return relu(v);
    } else if (type == 1) {
        return sigmoid(v);
    } else if (type == 2){
        return tanh(v);
    } else {
        return v;
    }
}